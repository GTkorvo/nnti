#!/usr/bin/perl -w
# /Trilinos/commonTools/test/utilities/runconfigure

################################################################################
# The Trilinos Project - runconfigure
# 
# Mike Phenow, Jim Willenbring
#
# This is a utility for configuring Trilinos.  It requires you to provide a
# complete invoke-configure file.  It is of limited value by itself.  It is
# best used in conjunction with runmake, as in the utility runbuild.
#
# - Check contents of report.
# - Improve comments.
# - Should dependencies be moved into packages like test definitions?
# - Do we need a dependencies file (or the same one) to specify which package
#   directories to blow away?
# - Output package names as they configure successfully.
#
################################################################################

use strict;

# Variable Declarations ========================================================

# Command line arguments:

my $trilinosDir;        # Trilinos directory        (required argument) 
my $buildDir;           # build directory           (required argument) 
my $invokeConfigure;    # complete invoke-configure (required argument)
my $recover;            # recover mode?
my $makeFailure;        # return code from runmake

my $outputDir;          # output directory          (default: .)
my $verbosity;          # verbosity level           (default: 1)
my $logVerbosity;       # log file verbosity level  (default: 0)

my $resultsDir;         # absolute path to results directory
my %dependencies;       # package dependencies

my $runStartTime;
my $runStartTimeForFilename;

# Constants
my $v0 = "0";           # quiet
my $v1 = "1";           # normal verbosity
my $v2 = "2";           # level 2 verbosity
my $v3 = "4";           # level 3 verbosity

# Package failure exit status codes.  These are needed when you are combining
# runconfigure and runmake (as in runbuild) and you want to be able to recover
# from make failures because runmake can't recover from a make failure by
# itself--it needs to communicate the broken package to runconfigure to that
# runconfigure can remove its directory and its configure flags.
my %failureCode = ();
$failureCode{'amesos'} = 10;
$failureCode{'anasazi'} = 11;
$failureCode{'aztecoo'} = 12;
$failureCode{'belos'} = 13;
$failureCode{'capo'} = 14;
$failureCode{'claps'} = 15;
$failureCode{'cmmlib'} = 16;
$failureCode{'didasko'} = 17;
$failureCode{'epetra'} = 18;
$failureCode{'epetraext'} = 19;
$failureCode{'ifpack'} = 20;
$failureCode{'jpetra'} = 21;
$failureCode{'kokkos'} = 22;
$failureCode{'komplex'} = 23;
$failureCode{'meros'} = 24;
$failureCode{'ml'} = 25;
$failureCode{'new_package'} = 26;
$failureCode{'nox'} = 27;
$failureCode{'pliris'} = 28;
$failureCode{'pytrilinos'} = 29;
$failureCode{'rythmos'} = 30;
$failureCode{'teuchos'} = 31;
$failureCode{'thyra'} = 32;
$failureCode{'tpetra'} = 33;
$failureCode{'triutils'} = 34;
$failureCode{'tsf'} = 35;
$failureCode{'tsfcore'} = 36;
$failureCode{'tsfcoreutils'} = 37;
$failureCode{'tsfextended'} = 38;
my %failedPackage = reverse(%failureCode);
        
################################################################################
# Execution ####################################################################
################################################################################

getArgs();
init();
parseDependencies();

my $exitStatus = run();
cleanUp();
exit $exitStatus;

################################################################################
# Subroutines ##################################################################
################################################################################

    ############################################################################
    # getArgs()
    #
    # Parse command line arguments.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub getArgs {
        
        # Argument variables that don't need to be global.
        my $quiet;
        my $help;

        # Gather command line arguments.
        use Getopt::Long;
        GetOptions( "trilinos-dir=s" => \$trilinosDir,
                    "build-dir=s" => \$buildDir,
                    "invoke-configure=s" => \$invokeConfigure,
                    "recover" => \$recover,
                    "make-failure=s" => \$makeFailure,
                    "output-dir=s" => \$outputDir,
                    "verbosity=i" => \$verbosity,
                    "log-verbosity=i" => \$logVerbosity,
                    "quiet" => \$quiet,
                    "help" => \$help );
        
        # Print help and exit.
        if ($help) { 
            printHelp();
            exit;
        }
        
        # Enforce and/or prepare arguments.
        
        # check for existance of trilinos-dir argument and actual directory
        if (!$trilinosDir) {
            die "trilinos-dir value required, see --help for more information\n"; 
        } else {
            if (!stat($trilinosDir)) {
                die "cannot stat trilinos-dir: $trilinosDir\n";
            }
        }
        
        # Check for existance of build-dir argument and actual directory.
        # If it isn't given as an absolute path, prepend the Trilinos path.
        # Blow it away and create it.
        if (!$buildDir) {
            die "build-dir value required, see --help for more information\n"; 
        } else {
            if ($buildDir !~ m/^\//) {
                $buildDir = "$trilinosDir/$buildDir";
            }
            if (!$makeFailure) {
                system ("rm -rf $buildDir");
                system("mkdir $buildDir") == 0 or die "cannot create build-dir: $buildDir\n";
            }
        }
        
        # Check for existance of invoke-configure argument and actual file
        if (!$invokeConfigure) {
            die "invoke-configure value required, see --help for more information\n"; 
        } else {
            if ($invokeConfigure !~ m/^\//) {
                $invokeConfigure = "$trilinosDir/commonTools/test/utilities/$invokeConfigure";
            }
            if (!stat($invokeConfigure)) {
                die "cannot stat invoke-configure: $invokeConfigure\n";
            } 
        }
        
        # Prevent problems with uninitialized value
        if (!$recover) {
            $recover = "";
        }
        
        # Check for existance of output directory, use it to create the
        # complete path for the results directory, and create the results
        # directory.
        if (!$outputDir) {
            $outputDir = "";
            $resultsDir = "$trilinosDir/commonTools/test/utilities/results"; 
        } else {
            if ($outputDir =~ m/^\//) {
                $resultsDir = $outputDir;
            } else {
                $resultsDir = "$trilinosDir/commonTools/test/utilities/$outputDir";
            }
        }
        if (!stat($resultsDir)) {
            system("mkdir $resultsDir") == 0 or die "cannot create $resultsDir, died";
        }
        
        # Set verbosity level to corresponding constant.  0, 1, 2, and 3 are
        # used for the levels the user may specify, but they are stored as 0,
        # 1, 2, and 4 so they can be combined and processed with bitwise
        # operations.
        if ($verbosity) {
            if      ($verbosity == 0) { $verbosity = $v0; }
            elsif   ($verbosity == 1) { $verbosity = $v1; }
            elsif   ($verbosity == 2) { $verbosity = $v2; }
            elsif   ($verbosity == 3) { $verbosity = $v3; }
        } else {
            $verbosity = $v1; 
        }
        
        # Set log verbosity level to corresponding constant.  Numbering scheme
        # is the same for the log verbosity as it is for the standard verbosity.
        # There is not distinct output and log output, the if a log verbosity
        # is given, then that level of output is written to a file instead of
        # to standard out.  
        if ($logVerbosity) {
            if      ($logVerbosity == 0) { $logVerbosity = $v0; }
            elsif   ($logVerbosity == 1) { $logVerbosity = $v1; }
            elsif   ($logVerbosity == 2) { $logVerbosity = $v2; }
            elsif   ($logVerbosity == 3) { $logVerbosity = $v3; }        
        } else {
            $logVerbosity = $v1; 
        }
        
        # Set quiet mode--same as passing --verbosity=0.
        if ($quiet) {
            $verbosity = $v0; 
        }
        
    } # getArgs()

    ############################################################################
    # init()
    #
    # Prepares varibles.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub init {
    
        # Capture and format make start time.
        (my $se, my $mn, my $hr, my $da, my $mo, my $yr) = (localtime)[0..5];
        $yr = sprintf("%02d", $yr % 100);
        $mo = sprintf("%02d", $mo+1);
        $da = sprintf("%02d", $da);
        $hr = sprintf("%02d", $hr);
        $mn = sprintf("%02d", $mn);
        $se = sprintf("%02d", $se);
        $runStartTime = $yr."-".$mo."-".$da." ".$hr.":".$mn.":".$se;
        $runStartTimeForFilename = $yr."-".$mo."-".$da."_".$hr.".".$mn.".".$se;
        
        # Print list of variables for debugging.
        my $message = "";
        $message .= "init():\n";
        $message .= "  \$trilinosDir = $trilinosDir\n";
        $message .= "  \$buildDir = $buildDir\n";     
        $message .= "  \$invokeConfigure = $invokeConfigure\n";      
        $message .= "  \$recover = $recover\n";
        $message .= "  \$outputDir = $outputDir\n";
        $message .= "  \$verbosity = $verbosity\n";
        $message .= "  \$logVerbosity = $logVerbosity\n";
        $message .= "  \$resultsDir = $resultsDir\n";
        $message .= "  \n";
        printMessage($message, $v3);
        
    } # init()
    
    ############################################################################
    # run()
    #
    # Moves into build-dir/ and configures.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub run {
        
        # Copy invoke-configure
        if (!$makeFailure) {
            system("cp $invokeConfigure $buildDir/invoke-configure") == 0
                or die "cannot copy invoke-configure, died";
        } else {
                
            # Move aside current, broken invoke-configure.  Serialize
            # broken invoke-configures so that all are preserved.
            my $icBrokenNumber = "00";
            while (stat "$buildDir/invoke-configure-broken-$icBrokenNumber") {
                $icBrokenNumber++;
            }
            system("cp $buildDir/invoke-configure $buildDir/invoke-configure-broken-$icBrokenNumber") == 0
                or die "can't copy invoke-configure, died";
                    
            removeBrokenPackage($failedPackage{$makeFailure});
            my $result = cleanInvokeConfigure($failedPackage{$makeFailure});
            if ($result == 86) { return 86; }
        }
        
        chdir $buildDir or die "cannot chdir to $buildDir, died";
            
        # grab contents of invoke-configure for reporting purposes.
        my $invokeConfigureString = "";              
        open (INVOKE_CONFIGURE, "<invoke-configure");
        undef $/;                                       # undefine input record separator
        $invokeConfigureString=<INVOKE_CONFIGURE>;      # copy entire file
        $/ = "\n";                                      # restore it to default newline
        close INVOKE_CONFIGURE;
            
        my $configurePassed = 0;
        my $eightySix;
        my $configLog = "";
        
        # Attempt to configure.  If --recover flag was passed, we will
        # continue to attempt to recover and reconfigure until it passes.
        while (!$configurePassed) {
    
            # Capture and format make start time.
            (my $se, my $mn, my $hr, my $da, my $mo, my $yr) = (localtime)[0..5];
            $yr = sprintf("%02d", $yr % 100);
            $mo = sprintf("%02d", $mo+1);
            $da = sprintf("%02d", $da);
            $hr = sprintf("%02d", $hr);
            $mn = sprintf("%02d", $mn);
            $se = sprintf("%02d", $se);
            $runStartTime = $yr."-".$mo."-".$da." ".$hr.":".$mn.":".$se;
            $runStartTimeForFilename = $yr."-".$mo."-".$da."_".$hr.".".$mn.".".$se;
            
            # Print progress message.
            my $message = "Configuring Trilinos...";
            $message = sprintf("%-30s", $message);
            printMessage($message, $v1+$v2+$v3);
            
            # Capture start time.
            my $startSeconds = time();
        
            # Run configure.  
            my $configureOutput = `./invoke-configure 2>&1`;
            my $configureExitStatus = $?;
        
            # Capture and format make stop time.
            ($se, $mn, $hr, $da, $mo, $yr) = (localtime)[0..5];
            $yr = sprintf("%02d", $yr % 100);
            $mo = sprintf("%02d", $mo+1);
            $da = sprintf("%02d", $da);
            $hr = sprintf("%02d", $hr);
            $mn = sprintf("%02d", $mn);
            $se = sprintf("%02d", $se);
            my $configureStopTime = $yr."-".$mo."-".$da." ".$hr.":".$mn.":".$se;
            my $stopSeconds = time();            
            my $runSeconds = $stopSeconds - $startSeconds;
            
            # Broken or not?
            my $brokenPackage = "NONE";
            my $configuredPackages = detectConfiguredPackages($configureOutput);
            if ($configureExitStatus) {
                
                # Move aside current, broken invoke-configure.  Serialize
                # broken invoke-configures so that all are preserved.
                my $icBrokenNumber = "00";
                while (stat "$buildDir/invoke-configure-broken-$icBrokenNumber") {
                    $icBrokenNumber++;
                }
                system("cp $buildDir/invoke-configure $buildDir/invoke-configure-broken-$icBrokenNumber") == 0
                    or die "can't copy invoke-configure, died";
            
                # Detect broken package, remove broken package directory, and
                # remove appropriate configure flags.
                $brokenPackage = detectBrokenPackage($configureOutput);                                
            
                # grab contents of broken package's config.log
                if ($brokenPackage ne "NONE" && $brokenPackage ne "UNKNOWN") {                
                    open (CONFIG_LOG, "<$buildDir/packages/$brokenPackage/config.log");
                    undef $/;                   # undefine input record separator
                    $configLog=<CONFIG_LOG>;    # copy entire file
                    $/ = "\n";                  # restore it to default newline
                    close CONFIG_LOG;
                }
                
                removeBrokenPackage($brokenPackage);
                my $result = cleanInvokeConfigure($brokenPackage);
                if ($result == 86) { $eightySix = 1; }
                
                $message = sprintf("%-25s", " broken: $brokenPackage ");
                printMessage($message, $v1+$v2+$v3);
                $message = "! FAILED ";
                printMessage($message, $v1+$v2+$v3);
            } else {
                $message = sprintf("%-25s", "");
                printMessage($message, $v1+$v2+$v3);
                $message = "  passed ";
                printMessage($message, $v1+$v2+$v3);
                $configurePassed = 1;
            }
            
            # Trim $configureOutput.
            $configureOutput = substr ($configureOutput, -1000000);
                                
            # Finish printing pass/fail message.
            $message = ($runSeconds==0?"<1":$runSeconds);
            $message = sprintf("%6s", $message);
            printMessage($message, $v1+$v2+$v3);
            $message = " second".($runSeconds>1?"s":"")."\n";
            printMessage($message, $v1+$v2+$v3);
                                                
        # Grab general information values for inclusion in machine info file. 
        my $hostName = "";          my $dnsName = "";           my $ipAddress = "";     
        my $operatingSystem = "";   my $kernelName = "";        my $kernelRelease = "";
        my $kernelVersion = "";     my $processor = "";         my $machineHardware = "";
        my $hardwarePlatform = "";  my $badCmd = 0;          
        $badCmd = system('hostname -s > /dev/null 2>&1');
        if (!$badCmd) { 
            chomp($hostName=`hostname -s`); 
        } else { 
            $badCmd = system('uname -n > /dev/null 2>&1');
            if (!$badCmd) { chomp($hostName=`uname -n`); }
        }
        $badCmd = system('hostname -d > /dev/null 2>&1');
        if (!$badCmd) { chomp($dnsName=`hostname -d`); }
        $badCmd = system('hostname -i > /dev/null 2>&1');
        if (!$badCmd) { chomp($ipAddress=`hostname -i`); }
        $badCmd = system('uname -o > /dev/null 2>&1');
        if (!$badCmd) { chomp($operatingSystem=`uname -o`); }
        $badCmd = system('uname -s > /dev/null 2>&1');
        if (!$badCmd) { chomp($kernelName=`uname -s`); }
        $badCmd = system('uname -r > /dev/null 2>&1');
        if (!$badCmd) { chomp($kernelRelease=`uname -r`); }
        $badCmd = system('uname -v > /dev/null 2>&1');
        if (!$badCmd) { chomp($kernelVersion=`uname -v`); }
        $badCmd = system('uname -p > /dev/null 2>&1');
        if (!$badCmd) { chomp($processor=`uname -p`); }
        $badCmd = system('uname -m > /dev/null 2>&1');
        if (!$badCmd) { chomp($machineHardware=`uname -m`); }
        $badCmd = system('uname -i > /dev/null 2>&1');
        if (!$badCmd) { chomp($hardwarePlatform=`uname -i`); }
            
            # Grab the repository branch tag.
            my $branchTag = "";
            my $homeDirContents = `ls $trilinosDir`;
            if ($homeDirContents =~ m/CVS/) {
                my $cvsDirContents = `ls $trilinosDir/CVS`;
                if ($cvsDirContents =~ m/Tag/) {
                    $branchTag = `cat $trilinosDir/CVS/Tag`;
                } else {
                    $branchTag = "development";
                }      
            } else {
                $branchTag = "unknown";
            }
            
            # grab contents of dependencies for reporting purposes.
            my $dependenciesString = "";              
            open (DEPENDENCIES, "<$trilinosDir/commonTools/test/utilities/dependencies");
            undef $/;                               # undefine input record separator
            $dependenciesString=<DEPENDENCIES>;     # copy entire file
            $/ = "\n";                              # restore it to default newline
            close DEPENDENCIES;
            
            # Create results file.
            my $resultsFile = "$resultsDir/$runStartTimeForFilename-configure.txt";
            open (RESULTS, ">$resultsFile")
                or die "can't open configure result file $resultsFile for writing, died";
                
            my $resultsString = "";
            
            $resultsString .= "HOST_NAME            = $hostName\n";
            $resultsString .= "DNS_NAME             = $dnsName\n";
            $resultsString .= "IP_ADDRESS           = $ipAddress\n";
            $resultsString .= "OPERATING_SYSTEM     = $operatingSystem\n";
            $resultsString .= "KERNEL_NAME          = $kernelName\n";
            $resultsString .= "KERNEL_RELEASE       = $kernelRelease\n";
            $resultsString .= "KERNEL_VERSION       = $kernelVersion\n";
            $resultsString .= "PROCESSOR            = $processor\n";
            $resultsString .= "MACHINE_HARDWARE     = $machineHardware\n";
            $resultsString .= "HARDWARE_PLATFORM    = $hardwarePlatform\n";
            $resultsString .= "\n";
            $resultsString .= "TRILINOS_DIR         = $trilinosDir\n";
            $resultsString .= "BRANCH_TAG           = $branchTag\n";
            $resultsString .= "\n";
            $resultsString .= "BUILD_DIR            = $buildDir\n";
            $resultsString .= "\n";
            $resultsString .= "START_TIME           = $runStartTime\n";
            $resultsString .= "STOP_TIME            = $configureStopTime\n";
            $resultsString .= "RUN_TIME             = ".($runSeconds==0?"<1":$runSeconds)." seconds\n";
            $resultsString .= "\n";
            $resultsString .= "EXIT_STATUS          = $configureExitStatus\n";
            $resultsString .= "RESULT               = ".($configureExitStatus==0?"pass":"fail")."\n";
            $resultsString .= "\n";
            $resultsString .= "BROKEN_PACKAGE       = $brokenPackage\n";
            $resultsString .= "\n";
            $resultsString .= "CONFIGURED_PACKAGES {{{\n\n$configuredPackages\n\n}}}\n";
            $resultsString .= "\n";
            $resultsString .= "CONFIGURE_OUTPUT {{{\n\n$configureOutput\n\n}}}\n";
            $resultsString .= "\n";
            $resultsString .= "CONFIG_LOG {{{\n\n$configLog\n\n}}}\n";
            $resultsString .= "\n";
            $resultsString .= "INVOKE_CONFIGURE {{{\n\n$invokeConfigureString\n\n}}}\n";
            $resultsString .= "\n";
            $resultsString .= "DEPENDENCIES {{{\n\n$dependenciesString\n\n}}}\n";
            
            print RESULTS $resultsString;
            
            close RESULTS;
            
            # Append to build info file for database.
            my $buildInfoFile = "$resultsDir/build.txt.tmp1";
            if (stat $buildInfoFile) {
                open (BUILD_FILE, ">>$buildInfoFile")
                    or die "can't open build info file $buildInfoFile for writing, died";            
                my $string = "";          
                $string .= "BRANCH_TAG           = $branchTag\n";  
                $string .= "DEPENDENCIES {{{\n\n$dependenciesString\n\n}}}\n\n";   
                print BUILD_FILE $string;        
                close BUILD_FILE;
                system("mv $buildInfoFile $resultsDir/build.txt.tmp2 > /dev/null 2>&1");
            }
                
            if (!$recover && $configureExitStatus) { return $failureCode{"$brokenPackage"}; }
                
            if ($eightySix) { return 86; }
            
        } # while(!$configurePassed)
        
        return 0;
        
    } # run()
    
    ############################################################################
    # detectBrokenPackage()
    #
    # Figure out which package failed to configure.
    #
    #   - args:     $configureOutput    (standard out and standard error from configure)
    #
    #   - returns:  $brokenPackage      (name of the package that failed to configure)
    #

    sub detectBrokenPackage {
        my $configureOutput = $_[0];
        
        my $brokenPackage;
        
        # Find last instance of "Running PACKAGE Configure Script."  Since 
        # configure failed, it should not have a matching "Finished Running..."
        # string, and PACKAGE should be the broken package.
        $configureOutput =~ m/.*^Running(.*?)Configure Script/ms;
        if (defined $1) { $brokenPackage = $1; }
        
        if (defined $brokenPackage) {
            $brokenPackage =~ s/^\s*//;             # trim leading spaces
            $brokenPackage =~ s/\s*$//;             # trim trailing spaces
            $brokenPackage = lc($brokenPackage);    # convert to lower-case
        } else {
            my $message = "error fixing invoke-configure--can't detect package\n";
            printMessage($message, $v1+$v2+$v3);
            return "UNKNOWN";
        }
        
        return $brokenPackage;
        
    } # detectBrokenPackage
    
    ############################################################################
    # detectConfiguredPackages()
    #
    # Figure out which packages configured successfully.
    #
    #   - args:     $configureOutput    (standard out and standard error from configure)
    #
    #   - returns:  $configuredPackages (list of packages that configured successfully)
    #

    sub detectConfiguredPackages {
        my $configureOutput = $_[0];
        
        my $configuredPackages = "";
        
        # Compile list of all "Finished Running PACKAGE Configure Script" strings.        
        my @finishedStrings = $configureOutput =~ m/(Finished( Running)? \w*? Configure Script)/g;        
        foreach my $finishedString (@finishedStrings) {
                            
            # Grab package name.
            my $package = "";
            if ($finishedString =~ m/Finished Running (\w*?) Configure Script/) {
                $package = $1;
                $package = lc($package);
                $configuredPackages .= "$package; ";
            }
            
        }
        
        return $configuredPackages;
        
    } # detectConfiguredPackages
    
    ############################################################################
    # cleanInvokeConfigure()
    #
    # Remove the given package and its dependents from the invoke-configure.
    #
    #   - args:     $brokenPackage    (name of package to clean from invoke-configure)
    #
    #   - returns:  NONE
    #

    sub cleanInvokeConfigure {
        my $brokenPackage = $_[0];
        
        # Open invoke-configure for reading.
        open (INVOKE_CONFIGURE, "<$buildDir/invoke-configure")
            or die "can't open invoke-configure, died";
            
        # Parse it, extracting lines to keep.
        my $newInvokeConfigure = "";
        my $changeMade = 0;
        while (my $line = <INVOKE_CONFIGURE>) {
            $line =~ s/^\s*//;  # trim leading spaces
            $line =~ s/\s*$//;  # trim trailing spaces
            
            # Compare lines to package dependencies.
            my $dropLine = 0;
            my $lastElementIndex = $#{$dependencies{$brokenPackage}};
            for (my $i=0; $i<=$lastElementIndex; $i++) { 
                if ($line =~ m/$dependencies{$brokenPackage}[$i]\b/i) {
                    $dropLine = 1;   
                    
                    # SPECIAL CASE # Nox / LOCA dependency issue
                    # removing --enable-loca is not enough, 
                    # we need to also manually add --disable-loca
                    if ($dependencies{$brokenPackage}[$i] =~ m/--enable-loca/) {
                        $newInvokeConfigure .= "--disable-loca \\\n";
                    }
                    
                }   
            }    
            
            # Write line if it isn't a dependency.
            if (!$dropLine) {                             
                $newInvokeConfigure .= "$line\n";
            } else {
                $changeMade = 1;
            }
        } # while ($line)        
        
        close INVOKE_CONFIGURE;
        
        if (!$changeMade) {
            my $message = "error fixing invoke-configure--no changes made ($brokenPackage broke)\n";
            printMessage($message, $v1+$v2+$v3);
            return 86;
        }
        
        # Prepare new invoke-configure.
        $newInvokeConfigure =~ s/(.*)\\$/$1/s;  # remove last line-continuation
        chomp ($newInvokeConfigure);            # remove last newline 
        
        # Open invoke-configure for writing.
        open (INVOKE_CONFIGURE, ">$buildDir/invoke-configure")
            or die "can't open invoke-configure, died";
        
        # Write new invoke configure.
        print INVOKE_CONFIGURE $newInvokeConfigure;
        close INVOKE_CONFIGURE; 
           
    } # cleanInvokeConfigure()
    
    ############################################################################
    # removeBrokenPackage()
    #
    # Blow away the given broken package.
    #
    #   - args:     $brokenPackage      (package to prune)
    #
    #   - returns:  NONE
    #

    sub removeBrokenPackage {
        my $brokenPackage = $_[0];
        
        # Standardized name to package directory name conversions.
        my %dirName = ();
        $dirName{'amesos'} = "amesos";
        $dirName{'anasazi'} = "anasazi";
        $dirName{'aztecoo'} = "aztecoo";
        $dirName{'belos'} = "belos";
        $dirName{'capo'} = "capo";
        $dirName{'claps'} = "claps";
        $dirName{'cmmlib'} = "cmmlib";
        $dirName{'didasko'} = "didasko";
        $dirName{'epetra'} = "epetra";
        $dirName{'epetraext'} = "epetraext";
        $dirName{'ifpack'} = "ifpack";
        $dirName{'jpetra'} = "jpetra";
        $dirName{'kokkos'} = "kokkos";
        $dirName{'komplex'} = "komplex";
        $dirName{'meros'} = "meros";
        $dirName{'ml'} = "ml";
        $dirName{'new_package'} = "new_package";
        $dirName{'nox'} = "nox";
        $dirName{'pliris'} = "pliris";
        $dirName{'pytrilinos'} = "PyTrilinos";
        $dirName{'rythmos'} = "rythmos";
        $dirName{'teuchos'} = "teuchos";
        $dirName{'thyra'} = "thyra";
        $dirName{'tpetra'} = "tpetra";
        $dirName{'triutils'} = "triutils";
        $dirName{'tsf'} = "TSF";
        $dirName{'tsfcore'} = "TSFCore";
        $dirName{'tsfcoreutils'} = "TSFCoreUtils";
        $dirName{'tsfextended'} = "TSFExtended";
        
        my $packageDir = "";
        
        $packageDir  = "$buildDir/packages/";
        $packageDir .= $dirName{"$brokenPackage"};        
        system("rm -rf $packageDir");
           
    } # removeBrokenPackage()
    
    ############################################################################
    # parseDependencies()
    #
    # Parses testharness/dependencies and fills global hash of arrays of the 
    # form ({PACKAGE_A, [optionA1, optionA2, ...]}, {PACKAGE_B, [optionB1, ...]})
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub parseDependencies {
        my $filename = "$trilinosDir/commonTools/test/utilities/dependencies";
        
        open (IN_FILE, "<$filename")
            or die "can't open $filename";
            
        my $line;
        my $name;
        my $value;
        my $continue;
        while ($line = <IN_FILE>) {
            $line =~ s/^\s*//;      # trim leading spaces
            $line =~ s/\s*$//;      # trim trailing spaces
            
            if (!$line || $line =~ m/^[# ]/) {
                # skip comments and blank lines
            } else {
                if ($continue) {    # previous line ended with a (\) continuation char
                    $line =~ m/^((\S*?\s*?|".*?"\s*?)*)\s*?(\\?)$/;
                    $value = $1;
                    $continue = $3;                    
                    # print "\$value: $value\n\n"; # debugging   
                                     
                } else {            # expecting a $name [+]= $value [$value ...] pair
                    $line =~ m/^(\S+)\s*?\+?=\s*((\S*?\s*?|".*?"\s*?)*)\s*?(\\?)$/;
                    $name = $1;
                    $value = $2;
                    $continue = $4;                    
                    # print "\$name: $name, \$value: $value\n\n"; # debugging
                }
                
                if (!exists $dependencies{$name}) {  # if there isn't an option with this $name...
                    $dependencies{$name} = ();       # add a new one
                }
                
                while ($value) {
                    $value =~ s/^\s*//;  # trim leading spaces
                    $value =~ s/\s*$//;  # trim trailing spaces
                    # print "\$value: $value\n"; # debugging
                    
                    $value =~ m/^(".*?"|\S+)/;          # grab leftmost value
                    my $v = $1;                         # store temporarily in $v
                    $value =~ s/^$v//;                  # remove $v from remaining list 
                    $v =~ s/"//g;                       # remove any quotes from $v
                    push (@{$dependencies{$name}}, $v); # add $v to %options{$name}     
                }
            } # else (non-comment/blank)
        } # while ($line)
        
        close IN_FILE;
        
        # print the dependencies hash of arrays for debugging
        printMessage("\n\%dependencies:\n\n", $v3);
        for my $name (keys %dependencies) {
            my $lastElementIndex = $#{$dependencies{$name}};
            printMessage("  $name (".($lastElementIndex+1)."): \n", $v3);
            for my $i (0 .. $lastElementIndex) {
                printMessage("    $i = $dependencies{$name}[$i]\n", $v3);
            }
        }            
        
    } # parseDependencies()
    
    ############################################################################
    # cleanUp()
    #
    # Clean up environment variables, temp files, etc.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub cleanUp {
    
        # Currently, there is nothing to clean up, but I will leave this
        # subroutine here for potential future use.
           
    } # cleanUp()
    
    ############################################################################
    # printMessage()
    #
    # Prints an event if the verbosity is set.
    #
    #   - args:     $message        (message to be printed)
    #               $level          (verbosity level of message)
    #
    #   - returns:  NONE
    #

    sub printMessage {
        my $message = $_[0];
        my $level = $_[1];
        
        if ($verbosity & $level) {
            print $message;
        }
        
        if ($logVerbosity & $level) {
            my $log = $resultsDir."/log.txt";
            open (LOG, ">>$log")
                or die "can't open $log";
            print LOG $message;
            close LOG;
        }
    } # printMessage()

    ############################################################################
    # printHelp()
    #
    # Prints help output.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub printHelp {
        print "runconfigure - The Trilinos Configure Utility\n";
        print "\n";
        print "Usage:  perl runconfigure --trilinos-dir=/home/user/Trilinos --build-dir=MPI\n";
        print "\n";
        print "Options:\n";
        print "\n";
        print "  --trilinos-dir=DIR         Specify the absolute path to the top-level\n";
        print "                             Trilinos directory that contains this program.\n";
        print "                             Example: /home/user/Trilinos\n";
        print "                             REQUIRED.\n";
        print "\n";
        print "  --build-dir=DIR            Specify the name of the build directory where you\n";
        print "                             would like to configure Trilinos.  If a relative\n";
        print "                             path is given, it is assumed to be in the given\n";
        print "                             Trilinos directory.  IMPORTANT: regardless of\n";
        print "                             whether this is a relative or absolute path,\n";
        print "                             runconfigure will do an 'rm -rf' on this direcotry\n";
        print "                             to blow it away before creating it and proceeding.\n";
        print "                             REQUIRED.\n";
        print "\n";
        print "  --invoke-configure=FILE    Relative paths to a complete invoke-configure file.\n";
        print "                             REQUIRED.\n";
        print "\n";
        print "  --recover                  If this flag is present, runconfigure will attempt\n";
        print "                             to remove a broken package and its dependents and\n";
        print "                             continue until some subset configures\n";
        print "                             successfully.\n";
        print "\n";
        print "  --output-dir=DIR           Specify the directory in which to create the\n";
        print "                             directory containing the results.\n";
        print "                             Default: \".\"\n";
        print "\n";
        print "  --verbosity=LEVEL          0 = no non-fatal ouput (same as --quiet)\n";
        print "                             1 = normal output (default)\n";
        print "                             2 = level 2 verbosity\n";
        print "                             3 = level 3 verbosity\n";
        print "\n";
        print "  --log-verbosity=LEVEL      0 = no log\n";
        print "                             1 = normal output (default)\n";
        print "                             2 = level 2 verbosity\n";
        print "                             3 = level 3 verbosity\n";
        print "\n";
        print "  --quiet                    Produce no non-fatal output.\n";
        print "\n";
        print "  --help                     Print this help output and exit.\n";
        print "\n";
        print "Notes:\n";
        print "  - For more information, see README-runconfigure in\n";
        print "    Trilinos/commonTools/test/utilities/\n";
        print "    or visit http://software.sandia.gov/trilinos/developer/\n";
        print "\n";
    } # printHelp()