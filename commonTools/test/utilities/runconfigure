#!/usr/bin/perl -w
# /Trilinos/commonTools/test/utilities/runconfigure

################################################################################
# The Trilinos Project - runconfigure
# 
# Mike Phenow, Jim Willenbring
#
# - Should dependencies be moved into packages like test definitions?
# - Do we need a dependencies file (or the same one) to specify which package
#   directories to blow away?
#
################################################################################

use strict;

# Variable Declarations ========================================================

# Command line arguments:

my $trilinosdir;        # Trilinos directory        (required argument) 
my $builddir;           # build directory           (required argument) 
my $invokeConfigure;    # complete invoke-configure (required argument)
my $recover;            # recover mode?
my $makeFailure;        # return code from runmake

my $outputdir;          # output directory          (default: .)
my $verbosity;          # verbosity level           (default: 1)
my $logverbosity;       # log file verbosity level  (default: 0)

my $resultsDir;         # absolute path to results directory
my %dependencies;       # package dependencies

my $runStartTime;
my $runStartTimeForFilename;

# Constants
my $v0 = "0";           # quiet
my $v1 = "1";           # normal verbosity
my $v2 = "2";           # level 2 verbosity
my $v3 = "4";           # level 3 verbosity

# Package Failure Exit Status Codes
my %failureCode = ();
$failureCode{'amesos'} = 10;
$failureCode{'anasazi'} = 11;
$failureCode{'aztecoo'} = 12;
$failureCode{'belos'} = 13;
$failureCode{'claps'} = 14;
$failureCode{'cmmlib'} = 15;
$failureCode{'didasko'} = 16;
$failureCode{'epetra'} = 17;
$failureCode{'epetraext'} = 18;
$failureCode{'ifpack'} = 19;
$failureCode{'jpetra'} = 20;
$failureCode{'kokkos'} = 21;
$failureCode{'komplex'} = 22;
$failureCode{'meros'} = 23;
$failureCode{'ml'} = 24;
$failureCode{'new_package'} = 25;
$failureCode{'nox'} = 26;
$failureCode{'pliris'} = 27;
$failureCode{'pytrilinos'} = 28;
$failureCode{'rythmos'} = 29;
$failureCode{'teuchos'} = 30;
$failureCode{'thyra'} = 31;
$failureCode{'tpetra'} = 32;
$failureCode{'triutils'} = 33;
$failureCode{'tsf'} = 34;
$failureCode{'tsfcore'} = 35;
$failureCode{'tsfcoreutils'} = 36;
$failureCode{'tsfextended'} = 37;
my %failedPackage = reverse(%failureCode);
        
################################################################################
# Execution ####################################################################
################################################################################

getArgs();
init();
parseDependencies();

my $exitStatus = run();
cleanUp();
exit $exitStatus;

################################################################################
# Subroutines ##################################################################
################################################################################

    ############################################################################
    # getArgs()
    #
    # Parse command line arguments.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub getArgs {
        
        my $quiet;
        my $help;

        use Getopt::Long;
        GetOptions( "trilinosdir=s" => \$trilinosdir,
                    "builddir=s" => \$builddir,
                    "invoke-configure=s" => \$invokeConfigure,
                    "recover" => \$recover,
                    "make-failure=s" => \$makeFailure,
                    "outputdir=s" => \$outputdir,
                    "verbosity=i" => \$verbosity,
                    "logverbosity=i" => \$logverbosity,
                    "quiet" => \$quiet,
                    "help" => \$help );
        
        # Print help and exit.
        if ($help) { 
            printHelp();
            exit;
        }
        
        # Enforce and/or prepare arguments.
        
        # check for existance of trilinosdir argument and actual directory
        if (!$trilinosdir) {
            die "trilinosdir value required, see --help for more information\n"; 
        } else {
            if (!stat($trilinosdir)) {
                die "cannot stat trilinosdir: $trilinosdir\n";
            }
        }
        
        # Fix makeFailure value
        if ($makeFailure) {
            $makeFailure = $makeFailure >> 8;
        }
        
        # Check for existance of builddir argument and actual directory.
        if (!$builddir) {
            die "builddir value required, see --help for more information\n"; 
        } else {
            if ($builddir !~ m/^\//) {
                $builddir = "$trilinosdir/$builddir";
            }
            if (!$makeFailure) {
                system ("rm -rf $builddir");
                mkdir($builddir) or die "cannot create builddir: $builddir\n";
            }
        }
        
        # Check for existance of invoke-configure argument and actual file
        if (!$invokeConfigure) {
            die "invoke-configure value required, see --help for more information\n"; 
        } else {
            if ($invokeConfigure !~ m/^\//) {
                $invokeConfigure = "$trilinosdir/commonTools/test/utilities/$invokeConfigure";
            }
            if (!stat($invokeConfigure)) {
                die "cannot stat invoke-configure: $invokeConfigure\n";
            } 
        }
        
        # Prevent problems with uninitialized value
        if (!$recover) {
            $recover = "";
        }
        
        # Check for existance of output directory.
        if (!$outputdir) {
            $outputdir = "";
            $resultsDir = "$trilinosdir/commonTools/test/utilities/results"; 
        } else {
            if ($outputdir =~ m/^\//) {
                $resultsDir = $outputdir;
            } else {
                $resultsDir = "$trilinosdir/commonTools/test/utilities/$outputdir";
            }
        }
        if (!stat($resultsDir)) {
            mkdir($resultsDir) or die "cannot create $resultsDir, died";
        }
        
        # Set verbosity level to corresponding constant.
        if ($verbosity) {
            if      ($verbosity == 0) { $verbosity = $v0; }
            elsif   ($verbosity == 1) { $verbosity = $v1; }
            elsif   ($verbosity == 2) { $verbosity = $v2; }
            elsif   ($verbosity == 3) { $verbosity = $v3; }
        } else {
            $verbosity = $v1; 
        }
        
        # Set log verbosity level to corresponding constant.
        if ($logverbosity) {
            if      ($logverbosity == 0) { $logverbosity = $v0; }
            elsif   ($logverbosity == 1) { $logverbosity = $v1; }
            elsif   ($logverbosity == 2) { $logverbosity = $v2; }
            elsif   ($logverbosity == 3) { $logverbosity = $v3; }        
        } else {
            $logverbosity = $v0; 
        }
        
        # Set quiet mode.
        if ($quiet) {
            $verbosity = $v0; 
        }
        
    } # getArgs()

    ############################################################################
    # init()
    #
    # Prepares varibles.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub init {
    
        # Capture and format make start time.
        (my $se, my $mn, my $hr, my $da, my $mo, my $yr) = (localtime)[0..5];
        $yr = sprintf("%02d", $yr % 100);
        $mo = sprintf("%02d", $mo+1);
        $da = sprintf("%02d", $da);
        $hr = sprintf("%02d", $hr);
        $mn = sprintf("%02d", $mn);
        $se = sprintf("%02d", $se);
        $runStartTime = $yr."-".$mo."-".$da." ".$hr.":".$mn.":".$se;
        $runStartTimeForFilename = $yr."-".$mo."-".$da."_".$hr.".".$mn.".".$se;
        
        # Print list of variables for debugging.
        my $message = "";
        $message .= "init():\n";
        $message .= "  \$trilinosdir = $trilinosdir\n";
        $message .= "  \$builddir = $builddir\n";     
        $message .= "  \$invokeConfigure = $invokeConfigure\n";      
        $message .= "  \$recover = $recover\n";
        $message .= "  \$outputdir = $outputdir\n";
        $message .= "  \$verbosity = $verbosity\n";
        $message .= "  \$logverbosity = $logverbosity\n";
        $message .= "  \$resultsDir = $resultsDir\n";
        $message .= "  \n";
        printMessage($message, $v3);
        
    } # init()
    
    ############################################################################
    # run()
    #
    # Moves into builddir/ and configures.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub run {
        
        # Copy invoke-configure
        if (!$makeFailure) {
            system("cp $invokeConfigure $builddir/invoke-configure") == 0
                or die "cannot copy invoke-configure, died";
        } else {
            print "\n\$failedPackage{$makeFailure}: ".$failedPackage{$makeFailure}."\n\n";
            removeBrokenPackage($failedPackage{$makeFailure});
            cleanInvokeConfigure($failedPackage{$makeFailure});
        }
        
        chdir $builddir or die "cannot chdir to $builddir, died";
            
        my $configurePassed = 0;
        
        while (!$configurePassed) {
            
            # Print progress message.
            my $message = "Configuring Trilinos...";
            $message = sprintf("%-30s", $message);
            printMessage($message, $v1+$v2+$v3);
            
            my $startSeconds = time();
        
            # Run configure        
            my $configureOutput = `./invoke-configure 2>&1`;
            my $configureExitStatus = $?;
        
            # Capture and format make stop time.
            (my $se, my $mn, my $hr, my $da, my $mo, my $yr) = (localtime)[0..5];
            $yr = sprintf("%02d", $yr % 100);
            $mo = sprintf("%02d", $mo+1);
            $da = sprintf("%02d", $da);
            $hr = sprintf("%02d", $hr);
            $mn = sprintf("%02d", $mn);
            $se = sprintf("%02d", $se);
            my $configureStopTime = $yr."-".$mo."-".$da." ".$hr.":".$mn.":".$se;
            my $stopSeconds = time();            
            my $runSeconds = $stopSeconds - $startSeconds;
            
            # Detect broken package.
            my $brokenPackage = "NONE";
            if ($configureExitStatus) {
                    
                my $icBrokenNumber = "00";
                while (stat "$builddir/invoke-configure-broken-$icBrokenNumber") {
                    $icBrokenNumber++;
                }
                system("cp $builddir/invoke-configure $builddir/invoke-configure-broken-$icBrokenNumber") == 0
                    or die "can't copy invoke-configure, died";
            
                $brokenPackage = detectBrokenPackage($configureOutput);
                removeBrokenPackage($brokenPackage);
                cleanInvokeConfigure($brokenPackage);
                
                $message = sprintf("%-25s", " broken: $brokenPackage ");
                printMessage($message, $v1+$v2+$v3);
                $message = "! FAILED ";
                printMessage($message, $v1+$v2+$v3);
            } else {
                $message = sprintf("%-25s", "");
                printMessage($message, $v1+$v2+$v3);
                $message = "  passed ";
                printMessage($message, $v1+$v2+$v3);
                $configurePassed = 1;
            }
                                
            # Finish printing pass/fail message.
            $message = ($runSeconds==0?"<1":$runSeconds);
            $message = sprintf("%6s", $message);
            printMessage($message, $v1+$v2+$v3);
            $message = " second".($runSeconds>1?"s":"")."\n";
            printMessage($message, $v1+$v2+$v3);
                                    
            # Grab general information values for inclusion in test results files.        
            chomp (my $hostName=`uname -n`);
            chomp (my $dnsName=`hostname -d`);
            chomp (my $ipAddress=`hostname -i`);
            chomp (my $operatingSystem=`uname -o`);
            chomp (my $kernelName=`uname -s`);
            chomp (my $kernelRelease=`uname -r`);
            chomp (my $kernelVersion=`uname -v`);
            chomp (my $processor=`uname -p`);
            chomp (my $machineHardware=`uname -m`);
            chomp (my $hardwarePlatform=`uname -i`);
            
            # Grab the repository branch tag
            my $branchTag = "";
            my $homeDirContents = `ls $trilinosdir`;
            if ($homeDirContents =~ m/CVS/) {
                my $cvsDirContents = `ls $trilinosdir/CVS`;
                if ($cvsDirContents =~ m/Tag/) {
                    $branchTag = `cat $trilinosdir/CVS/Tag`;
                } else {
                    $branchTag = "development";
                }      
            } else {
                $branchTag = "unknown";
            }
            
            # Create results file.
            my $resultsFile = "$resultsDir/configure_$runStartTimeForFilename.txt";
            open (RESULTS, ">$resultsFile")
                or die "can't open configure result file $resultsFile for writing, died";
                
            my $resultsString = "";
            
            $resultsString .= "HOST_NAME            = $hostName\n";
            $resultsString .= "DNS_NAME             = $dnsName\n";
            $resultsString .= "IP_ADDRESS           = $ipAddress\n";
            $resultsString .= "OPERATING_SYSTEM     = $operatingSystem\n";
            $resultsString .= "KERNEL_NAME          = $kernelName\n";
            $resultsString .= "KERNEL_RELEASE       = $kernelRelease\n";
            $resultsString .= "KERNEL_VERSION       = $kernelVersion\n";
            $resultsString .= "PROCESSOR            = $processor\n";
            $resultsString .= "MACHINE_HARDWARE     = $machineHardware\n";
            $resultsString .= "HARDWARE_PLATFORM    = $hardwarePlatform\n";
            $resultsString .= "\n";
            $resultsString .= "TRILINOS_DIR         = $trilinosdir\n";
            $resultsString .= "BRANCH_TAG           = $branchTag\n";
            $resultsString .= "\n";
            $resultsString .= "BUILD_DIR            = $builddir\n";
            $resultsString .= "\n";
            $resultsString .= "START_TIME           = $runStartTime\n";
            $resultsString .= "STOP_TIME            = $configureStopTime\n";
            $resultsString .= "RUN_TIME             = ".($runSeconds==0?"<1":$runSeconds)." seconds\n";
            $resultsString .= "\n";
            $resultsString .= "EXIT_STATUS          = $configureExitStatus\n";
            $resultsString .= "RESULT               = ".($configureExitStatus==0?"pass":"fail")."\n";
            $resultsString .= "\n";
            $resultsString .= "BROKEN_PACKAGE       = $brokenPackage\n";
            $resultsString .= "\n";
            $resultsString .= "BEGIN_CONFIGURE_OUTPUT\n";
            $resultsString .= "\n";
            $resultsString .= "$configureOutput\n";
            $resultsString .= "\n";
            $resultsString .= "END_CONFIGURE_OUTPUT\n";
            $resultsString .= "\n";
            
            print RESULTS $resultsString;
            
            close RESULTS;
                
            if (!$recover && $configureExitStatus) { return $failureCode{"$brokenPackage"}; }
            
        } # while(!$configurePassed)
        
        return 0;
        
    } # run()
    
    ############################################################################
    # detectBrokenPackage()
    #
    # Figure out which package failed to configure.
    #
    #   - args:     $configureOutput    (standard out and standard error from configure)
    #
    #   - returns:  $brokenPackage      (name of the package that failed to configure)
    #

    sub detectBrokenPackage {
        my $configureOutput = $_[0];
        
        my $brokenPackage;
        
        $configureOutput =~ m/.*^Running(.*?)Configure Script/ms;
        if (defined $1) { $brokenPackage = $1; }
        
        if (defined $brokenPackage) {
            $brokenPackage =~ s/^\s*//;             # trim leading spaces
            $brokenPackage =~ s/\s*$//;             # trim trailing spaces
            $brokenPackage = lc($brokenPackage);    # convert to lower-case
        } else {
            my $message = "error fixing invoke-configure--can't detect package\n";
            printMessage($message, $v1+$v2+$v3);
            return "UNKNOWN";
        }
        
        # return broken package name
        return $brokenPackage;
        
    } # detectBrokenPackage
    
    ############################################################################
    # cleanInvokeConfigure()
    #
    # Remove the given package and its dependents from the invoke-configure.
    #
    #   - args:     $brokenPackage    (name of package to clean from invoke-configure)
    #
    #   - returns:  NONE
    #

    sub cleanInvokeConfigure {
        my $brokenPackage = $_[0];
        
        # open invoke-configure for reading
        open (INVOKE_CONFIGURE, "<$builddir/invoke-configure")
            or die "can't open invoke-configure, died";
            
        # parse it, extracting lines to keep
        my $newInvokeConfigure = "";
        my $changeMade = 0;
        while (my $line = <INVOKE_CONFIGURE>) {
            $line =~ s/^\s*//;  # trim leading spaces
            $line =~ s/\s*$//;  # trim trailing spaces
            
            # compare lines to package dependencies
            my $dropLine = 0;
            my $lastElementIndex = $#{$dependencies{$brokenPackage}};
            for (my $i=0; $i<=$lastElementIndex; $i++) { 
                if ($line =~ m/$dependencies{$brokenPackage}[$i]\b/i) {
                    $dropLine = 1;   
                    
                    # SPECIAL CASE # Nox / LOCA dependency issue
                    # removing --enable-loca is not enough, 
                    # we need to also manually add --disable-loca
                    if ($dependencies{$brokenPackage}[$i] =~ m/--enable-loca/) {
                        $newInvokeConfigure .= "--disable-loca \\\n";
                    }
                    
                }   
            }    
            
            # write line if it isn't a dependency
            if (!$dropLine) {                             
                $newInvokeConfigure .= "$line\n";
            } else {
                $changeMade = 1;
            }
        } # while ($line)        
        
        close INVOKE_CONFIGURE;
        
        if (!$changeMade) {
            my $message = "error fixing invoke-configure--no changes made ($brokenPackage broke)\n";
            printMessage($message, $v1+$v2+$v3);
            return $brokenPackage;
        }
        
        # prepare new invoke-configure
        $newInvokeConfigure =~ s/(.*)\\$/$1/s;  # remove last line-continuation
        chomp ($newInvokeConfigure);            # remove last newline 
        
        # open invoke-configure for writing
        open (INVOKE_CONFIGURE, ">$builddir/invoke-configure")
            or die "can't open invoke-configure, died";
        
        # write new invoke configure
        print INVOKE_CONFIGURE $newInvokeConfigure;
        close INVOKE_CONFIGURE; 
           
    } # cleanInvokeConfigure()
    
    ############################################################################
    # removeBrokenPackage()
    #
    # Blow away the given broken package.
    #
    #   - args:     $brokenPackage      (package to prune)
    #
    #   - returns:  NONE
    #

    sub removeBrokenPackage {
        my $brokenPackage = $_[0];
        
        my %dirName = ();
        $dirName{'amesos'} = "amesos";
        $dirName{'anasazi'} = "anasazi";
        $dirName{'aztecoo'} = "aztecoo";
        $dirName{'belos'} = "belos";
        $dirName{'claps'} = "claps";
        $dirName{'cmmlib'} = "cmmlib";
        $dirName{'didasko'} = "didasko";
        $dirName{'epetra'} = "epetra";
        $dirName{'epetraext'} = "epetraext";
        $dirName{'ifpack'} = "ifpack";
        $dirName{'jpetra'} = "jpetra";
        $dirName{'kokkos'} = "kokkos";
        $dirName{'komplex'} = "komplex";
        $dirName{'meros'} = "meros";
        $dirName{'ml'} = "ml";
        $dirName{'new_package'} = "new_package";
        $dirName{'nox'} = "nox";
        $dirName{'pliris'} = "pliris";
        $dirName{'pytrilinos'} = "PyTrilinos";
        $dirName{'rythmos'} = "rythmos";
        $dirName{'teuchos'} = "teuchos";
        $dirName{'thyra'} = "thyra";
        $dirName{'tpetra'} = "tpetra";
        $dirName{'triutils'} = "triutils";
        $dirName{'tsf'} = "TSF";
        $dirName{'tsfcore'} = "TSFCore";
        $dirName{'tsfcoreutils'} = "TSFCoreUtils";
        $dirName{'tsfextended'} = "TSFExtended";
        
        my $packageDir = "";
        
        $packageDir  = "$builddir/packages/";
        $packageDir .= $dirName{"$brokenPackage"};        
        system("rm -rf $packageDir");
           
    } # removeBrokenPackage()
    
    ############################################################################
    # parseDependencies()
    #
    # Parses testharness/dependencies and fills global hash of arrays of the 
    # form ({PACKAGE_A, [optionA1, optionA2, ...]}, {PACKAGE_B, [optionB1, ...]})
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub parseDependencies {
        my $filename = "$trilinosdir/commonTools/test/utilities/dependencies";
        
        open (IN_FILE, "<$filename")
            or die "can't open $filename";
            
        my $line;
        my $name;
        my $value;
        my $continue;
        while ($line = <IN_FILE>) {
            $line =~ s/^\s*//;  # trim leading spaces
            $line =~ s/\s*$//;  # trim trailing spaces
            
            if (!$line || $line =~ m/^[# ]/) {
                # skip comments and blank lines
            } else {
                if ($continue) {    # previous line ended with a (\) continuation char
                    $line =~ m/^((\S*?\s*?|".*?"\s*?)*)\s*?(\\?)$/;
                    $value = $1;
                    $continue = $3;                    
                    # print "\$value: $value\n\n"; # debugging   
                                     
                } else {            # expecting a $name [+]= $value [$value ...] pair
                    $line =~ m/^(\S+)\s*?\+?=\s*((\S*?\s*?|".*?"\s*?)*)\s*?(\\?)$/;
                    $name = $1;
                    $value = $2;
                    $continue = $4;                    
                    # print "\$name: $name, \$value: $value\n\n"; # debugging
                }
                
                if (!exists $dependencies{$name}) {  # if there isn't an option with this $name...
                    $dependencies{$name} = ();       # add a new one
                }
                
                while ($value) {
                    $value =~ s/^\s*//;  # trim leading spaces
                    $value =~ s/\s*$//;  # trim trailing spaces
                    # print "\$value: $value\n"; # debugging
                    
                    $value =~ m/^(".*?"|\S+)/;          # grab leftmost value
                    my $v = $1;                         # store temporarily in $v
                    $value =~ s/^$v//;                  # remove $v from remaining list 
                    $v =~ s/"//g;                       # remove any quotes from $v
                    push (@{$dependencies{$name}}, $v); # add $v to %options{$name}     
                }
            } # else (non-comment/blank)
        } # while ($line)
        
        close IN_FILE;
        
        # print the dependencies hash of arrays for debugging
        printMessage("\n\%dependencies:\n\n", $v3);
        for my $name (keys %dependencies) {
            my $lastElementIndex = $#{$dependencies{$name}};
            printMessage("  $name (".($lastElementIndex+1)."): \n", $v3);
            for my $i (0 .. $lastElementIndex) {
                printMessage("    $i = $dependencies{$name}[$i]\n", $v3);
            }
        }            
        
    } # parseDependencies()
    
    ############################################################################
    # cleanUp()
    #
    # Clean up environment variables, temp files, etc.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub cleanUp {
           
    } # cleanUp()
    
    ############################################################################
    # printMessage()
    #
    # Prints an event if the verbosity is set.
    #
    #   - args:     $message        (message to be printed)
    #               $level          (verbosity level of message)
    #
    #   - returns:  NONE
    #

    sub printMessage {
        my $message = $_[0];
        my $level = $_[1];
        
        if ($verbosity & $level) {
            print $message;
        }
        
        if ($logverbosity & $level) {
            my $log = $resultsDir."make_$runStartTimeForFilename"."_log.txt";
            open (LOG, ">>$log")
                or die "can't open $log";
            print LOG $message;
            close LOG;
        }
    } # printMessage()

    ############################################################################
    # printHelp()
    #
    # Prints help output.
    #
    #   - args:     NONE
    #
    #   - returns:  NONE
    #

    sub printHelp {
        print "runconfigure - The Trilinos Configure Utility\n";
        print "\n";
        print "Usage:  perl runconfigure --trilinosdir=/home/user/Trilinos --buildir=MPI\n";
        print "\n";
        print "Options:\n";
        print "\n";
        print "  --trilinosdir=DIR          Specify the absolute path to the top-level\n";
        print "                             Trilinos directory that contains this program.\n";
        print "                             Example: /home/user/Trilinos\n";
        print "                             REQUIRED.\n";
        print "\n";
        print "  --builddir=DIR             Specify the name of the build directory where you\n";
        print "                             would like to configure Trilinos.  If a relative\n";
        print "                             path is given, it is assumed to be in the given\n";
        print "                             Trilinos directory.  IMPORTANT: regardless of\n";
        print "                             whether this is a relative or absolute path,\n";
        print "                             runconfigure will do an 'rm -rf' on this direcotry\n";
        print "                             to blow it away before creating it and proceeding.\n";
        print "                             REQUIRED.\n";
        print "\n";
        print "  --invoke-configure=FILE    Relative paths to a complete invoke-configure file.\n";
        print "                             REQUIRED.\n";
        print "\n";
        print "  --recover                  If this flag is present, runconfigure will attempt\n";
        print "                             to remove a broken package and its dependents and\n";
        print "                             continue until some subset configures\n";
        print "                             successfully.\n";
        print "\n";
        print "  --outputdir=DIR            Specify the directory in which to create the\n";
        print "                             directory containing the results.\n";
        print "                             Default: \".\"\n";
        print "\n";
        print "  --verbosity=LEVEL          0 = no non-fatal ouput (same as --quiet)\n";
        print "                             1 = normal output (default)\n";
        print "                             2 = level 2 verbosity\n";
        print "                             3 = level 3 verbosity\n";
        print "\n";
        print "  --logverbosity=LEVEL       0 = no log (default)\n";
        print "                             1 = normal output\n";
        print "                             2 = level 2 verbosity\n";
        print "                             3 = level 3 verbosity\n";
        print "\n";
        print "  --quiet                    Produce no non-fatal output.\n";
        print "\n";
        print "  --help                     Print this help output and exit.\n";
        print "\n";
        print "Notes:\n";
        print "  - For more information, see README-runconfigure in\n";
        print "    Trilinos/commonTools/test/utilities/\n";
        print "    or visit http://software.sandia.gov/trilinos/developer/\n";
        print "\n";
    } # printHelp()