#!/bin/csh
##############################################################################
# Zoltan Library for Parallel Applications                                   #
# Copyright (c) 2000,2001,2002, Sandia National Laboratories.                #
# For more info, see the README file in the top-level Zoltan directory.      # 
##############################################################################
##############################################################################
# CVS File Information
#    $RCSfile$
#    $Author$
#    $Date$
#    $Revision$
##############################################################################
#
# This C-shell script runs all the test examples 
# and compares the results against pre-computed results.
#
# Usage: test_zoltan [-arch arch-type] [-cmd command] 
#
# where arch-type is one of {generic, solaris, gcc, linux, tflop, cplant,
# dec, sgi32, sgi64}.
# Alternatively, give a run command with the -cmd option.
#

# Chaco problems to run
set ch_name = (simple nograph vwgt ewgt bug grid20x19 film hammond)
set ch_nproc = (4 4 3 4 3 5 6 8) # no. of procs

# Nemesis problems to run  
set nem_name = (box_064 ti_300 ti_4k)
set nem_nproc = (5 6 9) # no. of procs
set nem_pdisk = (1 2 2) # parallel disk info

# Set default options
@ no_chaco = 0
@ no_nemesis = 0
@ no_rcb = 0
@ no_rib = 0
@ no_hsfc = 0
@ no_bsfc = 0
@ no_oct = 0
@ no_parmetis = 0
@ yes_jostle = 0
@ yes_fortran = 0
set out_tag = out
set logfile = test_zoltan.log
set answers = answers
set outdir = output
unset run

# Check for environment variables.
if ($?ZOLTAN_ARCH) then
   set arch = $ZOLTAN_ARCH
endif
if ($?NO_NEMESIS) then
   set no_nemesis = $NO_NEMESIS
endif
if ($?NO_RCB) then
   set no_rcb = $NO_RCB
endif
if ($?NO_RIB) then
   set no_rib = $NO_RIB
endif
if ($?NO_HSFC) then
   set no_hsfc = $NO_HSFC
endif
if ($?NO_BSFC) then
   set no_bsfc = $NO_BSFC
endif
if ($?NO_OCT) then
   set no_oct = $NO_OCT
endif
if ($?NO_PARMETIS) then
   set no_parmetis = $NO_PARMETIS
endif
if ($?YES_JOSTLE) then
   set yes_jostle = $YES_JOSTLE
endif
if ($?YES_FORTRAN) then
   set yes_fortran = $YES_FORTRAN
   set no_nemesis = 1
   set out_tag = fout
endif

# Parse command-line options. 
# These options will override the environment variables.

while ( $#argv )

  if ( ("$1" == "-h") || ("$1" == "-help") ) then
     goto usage
  else if ( "$1" == "-no_nemesis" ) then
     @ no_nemesis = 1
  else if ( "$1" == "-no_chaco" ) then
     @ no_chaco = 1
  else if ( "$1" == "-no_rcb" ) then
     @ no_rcb = 1
  else if ( "$1" == "-no_rib" ) then
     @ no_rib = 1
  else if ( "$1" == "-no_hsfc" ) then
     @ no_hsfc = 1
  else if ( "$1" == "-no_bsfc" ) then
     @ no_bsfc = 1
  else if ( "$1" == "-no_oct" ) then
     @ no_oct = 1
  else if ( "$1" == "-no_parmetis" ) then
     @ no_parmetis = 1
  else if ( "$1" == "-yes_jostle" ) then
     @ yes_jostle = 1
  else if ( "$1" == "-yes_fortran" ) then
     @ yes_fortran = 1
     @ no_nemesis = 1
     set out_tag = fout
  else if ( "$1" == "-logfile" || "$1" == "-log" ) then
  
    if ( $#argv < 2 ) then
      echo "Error: No argument for $1"
      goto usage
    endif
    
    shift argv
    set logfile = "$1"
    
  else if ( "$1" == "-arch" ) then
  
    if ( $#argv < 2 ) then
      echo "Error: No argument for -arch"
      goto usage
    endif
    
    shift argv
    set arch = "$1"
    
  else if ( "$1" == "-cmd" ) then
    if ( $#argv < 2 ) then
      echo "Error: No argument for -cmd"
      goto usage
    endif
    
    shift argv
    set run = "$1"
  
  else if ( "$1" == "-exe" ) then
    if ( $#argv < 2 ) then
      echo "Error: No argument for -exe"
      goto usage
    endif
    
    shift argv
    set zdrive = "$1"
  
  endif
  
  shift argv

end # while ( $#argv )

# Determine what command to use to launch a parallel program based on $arch
if (! $?run ) then
  if ( $?arch ) then
    switch ( $arch )
    case generic:
    case gcc:
    case gccP3:
    case g++:
    case linux:
    case solaris:
    case sun:
    case sgi32:
    case sgi64:
      set run = "mpirun -np"
      breaksw
    case tflop:
      set run = "yod -proc 3 -sz"
      breaksw
    case cplant:
      set run = "yod -sz"
      breaksw
    case stratus:
      set run = "mpirun -np"
      breaksw
    case dec:
      set run = "dmpirun -np"
      breaksw
    default:
      echo "Unknown arch type. Please use -cmd to specify the run command."
      exit -2
      breaksw
    endsw
  else # !$?run and !$?arch
    echo "Error: No run command. Either -arch or -cmd must be specified."
    goto usage
  endif
endif
  

# Find the right zdrive to run
@ found = 0
if ( $yes_fortran ) then
# Using F90 
  if (! $?zdrive ) then
    set zdrive = zfdrive
  endif
  if ( $?arch ) then
    set zdrives = ( $zdrive ../Obj_${arch}/zfdrive )
  else
    set zdrives = $zdrive
  endif
  foreach zdrive ($zdrives)
    if ( -x $zdrive ) then
      echo "Found zdrive = $zdrive "
      @ found = 1
      break
    endif
  end
else
# Using C
  if (! $?zdrive ) then
    set zdrive = zdrive
  endif
  if ( $?arch ) then
    set zdrives = ( $zdrive ../Obj_${arch}/zdrive )
  else
    set zdrives = $zdrive
  endif
  foreach zdrive ($zdrives)
    if ( -x $zdrive ) then
      echo "Found zdrive = $zdrive "
      @ found = 1
      break
    endif
  end
endif

if (! $found) then
  echo "Error: Could not find zdrive executable"
  exit -3
endif

set ch_methods = ( )
set nem_methods = ( )

# Determine ParMETIS version, if any
if ( $no_parmetis ) then
  @ pmversion = 0
else
  # Scan ParMETIS header file for version info
  set pmfile = `sed -n 's/^.* \([^ ]*parmetis\.h\).*/\1/p' ../Obj_${arch}/parmetis_jostle.d`
  set majorversion = `grep MAJOR_VERSION $pmfile[1]`
  if ( $status ) then
    @ pmversion = 2
  else
    @ pmversion = $majorversion[3]
  endif
endif 

echo "ParMETIS version is $pmversion"

# Check for Jostle
if ( $yes_jostle ) then
  set ch_methods = ( $ch_methods jostle jostle-diffusion )
endif

# Methods to try
if ( $yes_fortran ) then
# Using F90
  if (! $no_rcb) then
    set ch_methods = ( $ch_methods rcb rcb-ts )
  endif
  if (! $no_rib) then
    set ch_methods = ( $ch_methods rib rib-ts )
  endif
  if (! $no_hsfc) then
    set ch_methods = ( $ch_methods hsfc hsfc-partless hsfc-partmore )
  endif
  if (! $no_bsfc) then
    set ch_methods = ( $ch_methods bsfc )
  endif
  if (! $no_oct) then
    set ch_methods = ( $ch_methods oct0 oct1 oct2 )
  endif
  if (! $no_parmetis) then
    if ( $pmversion == 2) then
      set ch_methods = ( $ch_methods partkway partgeom diffusion )
    else
      set ch_methods = ( $ch_methods partkway-v$pmversion partgeom-v$pmversion diffusion-v$pmversion )
    endif
  endif
else
# Using C

  # These Chaco problems are run only for C driver; F90 driver does not
  # read assignment input files.
  set ch_name = ($ch_name drake)
  set ch_nproc = ($ch_nproc 3) # no. of procs

  if (! $no_rcb) then
    set ch_methods = ( $ch_methods rcb rcb-oneproc rcb-ts )
    set nem_methods = ( $nem_methods rcb rcb-ts )
  endif
  if (! $no_rib) then
    set ch_methods = ( $ch_methods rib rib-oneproc rib-ts )
    set nem_methods = ( $nem_methods rib rib-ts )
  endif
  if (! $no_hsfc) then
    set ch_methods = ( $ch_methods hsfc hsfc-oneproc hsfc-partless hsfc-partmore hsfc-partlocal1 hsfc-partlocal2 )
    set nem_methods = ( $nem_methods hsfc hsfc-partless hsfc-partmore hsfc-partlocal1 hsfc-partlocal2 )
  endif
  if (! $no_bsfc) then
    set ch_methods = ( $ch_methods bsfc )
    set nem_methods = ( $nem_methods bsfc )
  endif
  if (! $no_oct) then
    set ch_methods = ( $ch_methods oct0 oct1 oct2 )
    set nem_methods = ( $nem_methods oct0 oct1 oct2 )
  endif
  if (! $no_parmetis) then
    if ( $pmversion == 2) then
      set ch_methods = ( $ch_methods partkway partkway-oneproc partkway-cyclic partgeom diffusion )
      set nem_methods = ( $nem_methods partkway partgeom diffusion )
    else
      set ch_methods = ( $ch_methods partkway-v$pmversion partkway-oneproc-v$pmversion partkway-cyclic-v$pmversion partgeom-v$pmversion diffusion-v$pmversion )
      set nem_methods = ( $nem_methods-v$pmversion partkway-v$pmversion partgeom-v$pmversion diffusion-v$pmversion )
    endif
  endif
endif

# Set up log file
if ( -e $logfile ) /bin/mv $logfile ${logfile}.old
echo "Test date  = `date`" > $logfile
echo "System     = `uname -a`" >> $logfile
echo "Arch       = ${arch}" >> $logfile
echo "Fortran    = $yes_fortran" >> $logfile
echo "Run cmd    = $run" >> $logfile
echo "Run dir    = $cwd" >> $logfile
echo "Executable = $zdrive" >> $logfile
echo " " >> $logfile


# Loop over Chaco problems
if (! $no_chaco ) then
  @ nprob = $#ch_name
  @ nfailed = 0
  while ( $#ch_name )

    # Go to the next test directory, initialize
    cd ch_${ch_name[1]}
    @ np = $ch_nproc[1]
    echo "Running test case ${ch_name[1]} on ${ch_nproc[1]} procs" | tee -a ../$logfile

    # Check output directories 
    if (! -d $outdir ) mkdir $outdir
    if (! -d $answers ) then
      echo "Warning: Answer directory missing in ch_${ch_name[1]}" | tee -a ../$logfile
    endif

    # Save generic input file 
    if (-e zdrive.inp ) /bin/mv zdrive.inp zdrive.inp.bak

    # Loop over all methods
    foreach mtd ( $ch_methods )
      # Clear fail flag
      @ fail = 0
      @ skip = 0

      # EBEB 11/2002: Removed those answer files. Hack below not necessary.
      # Temporary hack: We know ParMetis2 bombs on nograph, so skip these cases!
      # if ( (${ch_name[1]} == nograph) && ($pmversion == 2) ) then
      #    if (($mtd == partkway)  \
      #        || ($mtd == partkway-cyclic) \
      #        || ($mtd == partkway-oneproc) \
      #        || ($mtd == diffusion)) then
      #       @ skip = 1
      #    endif
      # endif

      if (! $skip ) then

        # Check input file
        if (-e zdrive.inp.${mtd} ) then
          /bin/cp zdrive.inp.${mtd} zdrive.inp
          if (-e ${outdir}/${ch_name[1]}.$mtd.$np.0 ) then
            /bin/rm ${outdir}/${ch_name[1]}.$mtd.$np.*
          endif

          # Run the driver
          $run $np ../$zdrive
          sleep 1 # Make sure all processes have time to write to disk
    
          # Save and compare output files 
          @ i = 0
          while ( $i < $np )
            /bin/mv ${ch_name[1]}.$out_tag.$np.$i  ${outdir}/${ch_name[1]}.$mtd.$np.$i
            if (-e ${answers}/${ch_name[1]}.$mtd.$np.$i.$arch ) then
       	      set answerfile = ${answers}/${ch_name[1]}.$mtd.$np.$i.$arch
            else
              set answerfile = ${answers}/${ch_name[1]}.$mtd.$np.$i
            endif
            diff -cw ${outdir}/${ch_name[1]}.$mtd.$np.$i $answerfile >> ../$logfile
            if ( $status ) then 
              @ fail = 1
            endif
            @ i = $i + 1
          end
    
        else
          echo "Warning: No input file ${ch_name[1]}/zdrive.inp.${mtd}, skipping..." | tee -a ../$logfile
        endif
  
  
        # Report success or failure 
        if ( $fail ) then
          @ nfailed = $nfailed + 1
          echo "Test problem ${ch_name[1]} with method ${mtd} FAILED" | tee -a ../$logfile
        else
          echo "Test problem ${ch_name[1]} with method ${mtd} OK" | tee -a ../$logfile 
        endif

      endif # !skip

    end # foreach method

    # Restore zdrive.inp
    if (-e zdrive.inp.bak ) /bin/mv zdrive.inp.bak zdrive.inp

    # Shift arguments and return to original directory
    shift ch_name
    shift ch_nproc
    cd ..
  end
  
  # Print failure summary
  echo " " | tee -a $logfile
  if ( $nfailed == 0 ) then
    echo "Success: all Chaco problems passed." | tee -a $logfile
  else
    echo "Failure: $nfailed Chaco problems failed." | tee -a $logfile
  endif
  echo " " | tee -a $logfile

endif # chaco

# Loop over Nemesis problems
if (! $no_nemesis ) then
  @ nprob = $#nem_name
  @ nfailed = 0
  while ( $#nem_name )

    # Go to the next test directory, initialize
    cd nem_${nem_name[1]}
    @ np = $nem_nproc[1]
    echo "Running test case ${nem_name[1]} on ${nem_nproc[1]} procs" | tee -a ../$logfile

    # Check output directories 
    if (! -d $outdir ) mkdir $outdir
    if (! -d $answers ) then
      echo "Warning: Answer directory missing in nem_${nem_name[1]}" | tee -a ../$logfile
    endif

    # Save input file 
    if (-e zdrive.inp ) /bin/mv zdrive.inp zdrive.inp.bak

    # Loop over all methods
    foreach mtd ( $nem_methods )
      # Clear fail flag
      @ fail = 0
      @ skip = 0

      if (! $skip ) then

        # Check input file
        if (-e zdrive.inp.${mtd} ) then
          /bin/cp zdrive.inp.${mtd} zdrive.inp
          /bin/rm ${outdir}/${nem_name[1]}.$mtd.$np.*
    
          # Run the driver
          $run $np ../$zdrive
    
          # Save and compare output files 
          @ i = 0
          @ pdirno = 1 # parallel subdirectory number
          while ( $i < $np )
            if ( $pdirno < 10 ) then
               set pdir = pio_0${pdirno}
            else
               set pdir = pio_${pdirno}
            endif
            /bin/mv ${pdir}/${nem_name[1]}-m${np}-r.par.out.$np.$i  ${outdir}/${nem_name[1]}.$mtd.$np.$i
            if (-e ${answers}/${nem_name[1]}.$mtd.$np.$i.$arch ) then
       	      set answerfile = ${answers}/${nem_name[1]}.$mtd.$np.$i.$arch
            else
              set answerfile = ${answers}/${nem_name[1]}.$mtd.$np.$i
            endif
            diff -cw ${outdir}/${nem_name[1]}.$mtd.$np.$i $answerfile >> ../$logfile
            if ( $status ) then 
              @ fail = 1
            endif
            @ i = $i + 1
            @ pdirno = $pdirno + 1
            if ($pdirno > $nem_pdisk[1]) then
               @ pdirno = 1
            endif
          end
    
        else
          echo "Warning: No input file ${nem_name[1]}/zdrive.inp.${mtd}, skipping..." | tee -a ../$logfile
        endif
  
        # Report success or failure 
        if ( $fail ) then
          @ nfailed = $nfailed + 1
          echo "Test problem ${nem_name[1]} with method ${mtd} FAILED" | tee -a ../$logfile
        else
          echo "Test problem ${nem_name[1]} with method ${mtd} OK" | tee -a ../$logfile 
        endif

      endif # !skip

    end # foreach method

    # Restore zdrive.inp
    if (-e zdrive.inp.bak ) /bin/mv zdrive.inp.bak zdrive.inp

    # Shift list arguments and return to original directory
    shift nem_name
    shift nem_nproc
    shift nem_pdisk
    cd ..
  end
  
  # Print failure summary
  echo " " | tee -a $logfile
  if ( $nfailed == 0 ) then
    echo "Success: all Nemesis problems passed." | tee -a $logfile
  else
    echo "Failure: $nfailed Nemesis problems failed." | tee -a $logfile
  endif
  echo " " | tee -a $logfile

endif # Nemesis

# Exit successfully
echo "Test final = `date`" >> $logfile
exit 0

# Print usage and exit with error
usage:
  echo "Usage: $0 [-arch arch-type] [-cmd command]"
  echo "where arch-type is one of {generic, gcc, sun, solaris, tflop, cplant, dec, linux, sgi32, sgi64}."
  echo "(If you have set ZOLTAN_ARCH you do not need to use -arch.)"
  echo "Alternatively, specify the command to load and run a parallel "
  echo "program with the -cmd option."
  echo "Other options:"
  echo "   -logfile filename"
  echo "   -exe filename"
  echo "   -no_rcb"
  echo "   -no_rib"
  echo "   -no_hsfc"
  echo "   -no_bsfc"
  echo "   -no_oct"
  echo "   -no_parmetis"
  echo "   -no_nemesis"
  echo "   -no_chaco"
  echo "   -yes_fortran"
  echo "   -yes_jostle"
  echo "The default is to run all methods (except Jostle) and file formats."
  exit -1

