#!/bin/csh -f

# inherit from cron_script
# setenv PATH /Net/local/gnu/bin:/bin:/usr/ucb:/usr/ccs/bin:/Net/local/bin:/usr/local/bin:$PATH

# todo: set appropriate revision

#
# inherited from run_fast -- added to config.fast
# $CVSCMD Zoltan

# fix issue with python in /usr/local
setenv LD_LIBRARY_PATH /usr/local/lib

# turn off globbing, to prevent expanding 'test/*' and similar args
set noglob

set dir=`pwd`
set fastdir="$dir/../../fast/client"
set driver="$fastdir/driver"

cd Zoltan
set zdir=`pwd`

# from config.fast:  lin64test $tname zdrive 'test/{ch_*,hg_*}'
set arch="$1"
set tname="$2"
set zdrive="$3"
set testdirs="$4"

set tag='nightly'
echo "$tname" | grep 'smoke' > /dev/null && set tag='smoke'


cat << _DONE_ >& $dir/test/config.out
Config:
    ZOLTAN_ARCH=$arch
    tag=$tag
    Test name=$tname
    zdrive=$zdrive
    testdirs=$testdirs
_DONE_

# check other args here
if ("$arch" == "--codecheck") then
  $driver --o=$dir/test --I=$fastdir --name="$tname" check --config=test/th/codechecks.cfg

else


  $driver --o=$dir/test --I=$fastdir --name="$tname" configure --logfile=$dir/test/config.out

  if ("$zdrive" == "zfdrive") then
    # Special setup required for building zfdrive -- the F90 driver.
    # Have to use appropriate zoltan_user_data.f90.
    # For now, just build the F90 driver zfdrive.  zfdrive requires the input
    # file to be called "zdrive.inp"; the current framework passes the input
    # file as a command-line argument.
    /bin/cp -p fdriver/zoltan_user_data.f90 fort/zoltan_user_data.f90 
    gmake "ZOLTAN_ARCH=$arch" zoltan "$zdrive" >& $dir/test/build.out

    $driver --o=$dir/test --I=$fastdir --name="$tname" build --logfile=$dir/test/build.out

  else 
    # For all other drivers (zdrive, zCPPdrive), build AND test.
    gmake "ZOLTAN_ARCH=$arch" zoltan "$zdrive" >& $dir/test/build.out

    $driver --o=$dir/test --I=$fastdir --name="$tname" build --logfile=$dir/test/build.out

    # create the test data, xml files, factor files
    ( cd test ; csh -f mktests >& $dir/mktest.out )

    # Test
    $driver --o=$dir/test --I=$fastdir --name="$tname" test --testdirs="$testdirs" --tag=$tag

  endif

endif
