dnl Process this file with autoconf 2.13 to produce a configure script
dnl for the CCA tutorial software installation.

#--------------------------------------------------------------------
#       Disable caching since it seems to get in the way
#       more times than it helps us.
#--------------------------------------------------------------------
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl

# Make sure we're us.
AC_INIT(include/Mesquite.hpp)

#------------------------------------------------------------------------------
# Directory where we keep the autoconf stuff and some HPC environment folklore.
#------------------------------------------------------------------------------
AC_CONFIG_AUX_DIR(./config_aux)

if test "${prefix}" = "NONE"; then
    prefix=/usr/local
fi
if test "${exec_prefix}" = "NONE"; then
    exec_prefix=$prefix
fi

# source tree vars
MSQ_ROOT=`pwd`
# The next two lines entirely quoted to keep autoconf/m4 
# from messing with the [] in the regex.
[MSQ_PARENT=`echo $MSQ_ROOT | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
[MSQ_GRANDPARENT=`echo $MSQ_PARENT | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
[MSQ_ANCESTOR=`echo $MSQ_GRANDPARENT | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
# install tree vars
MSQ_INSTALL_ROOT="$MSQ_ROOT"

#-------------------------------------------------------------
# Load in the Msq macros determined by the config/acsite.m4 file
#-------------------------------------------------------------

# Checks for unix programs
AC_DEFUN(MSQ_UNIX_PROGRAMS,
[
AC_CANONICAL_SYSTEM
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h strings.h sys/file.h sys/time.h unistd.h)

AC_PROG_LN_S
AC_SUBST(LN_S)

AC_PROG_MAKE_SET
AC_SUBST(SET_MAKE)

AC_PROG_RANLIB
AC_SUBST(RANLIB)


# source tree vars
CURRDIR=`pwd`
# The next two lines entirely quoted to keep autoconf/m4 
# from messing with the [] in the regex.
[CURRDIR_PARENT=`echo $CURRDIR | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
[CURRDIR_GRANDPARENT=`echo $CURRDIR_PARENT | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
[CURRDIR_GREATGRANDPARENT=`echo $CURRDIR_GRANDPARENT | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
[CURRDIR_ANCESTOR=`echo $CURRDIR_GREATGRANDPARENT | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`]
]
)

## end of paths to search collection

AC_DEFUN(MSQ_COMPILER,
[
#AC_LANG_CPLUSPLUS

# FIND C compiler
#------------------------------------------------------------------------------
#  see if the user gave us the location of the c compiler
#------------------------------------------------------------------------------
AC_ARG_WITH(cc,
    [Location of alternative c compiler:
  --with-cc=EXECUTABLE
                          The location of C.
                          Give the full path:
                              --with-cc='/usr/local/gcc/bin/gcc' ],
    , [with_cc=yes])

case "$with_cc" in
    no)
        #  User specified --without-cc
        #  print a warning.
        AC_MSG_WARN([Option '--without-cc' Causes demo to be suppressed.])
        AC_MSG_ERROR([A c compiler is required; demo has a c code.])
        ;;
    yes)
        #  User didn't give the option or didn't give useful
        #  information, search for it ourselves
        with_cc=_searching
        ;;
    *)
        CC="$with_cc"
        ;;
esac

if test "$with_cc" = _searching ; then
    AC_PROG_CC
fi
AC_SUBST(CC)

# FIND c++ compiler
#------------------------------------------------------------------------------
#  see if the user gave us the location of the c++ compiler
#------------------------------------------------------------------------------
AC_ARG_WITH(cPP,
    [Location of alternative c++ compiler:
  --with-cPP=EXECUTABLE
                          The location of C++.
                          Give the full path:
                              --with-cPP='/usr/local/gcc/bin/g++' ],
    , [with_cPP=yes])

case "$with_cPP" in
    no)
        #  User specified --without-c++
        #  print a warning.
        AC_MSG_WARN([Option '--without-cPP' Causes compilation to be suppressed.])
        AC_MSG_ERROR([A c++ compiler is required; current code is a c++ code.])
        ;;
    yes)
        #  User didn't give the option or didn't give useful
        #  information, search for it ourselves
        with_cPP=_searching
        ;;
    *)
        CXX="$with_cPP"
        ;;
esac

if test "$with_cPP" = _searching ; then
    AC_PROG_CXX
fi

# FIND c++ preprocessor
AC_PROG_CXXCPP
AC_SUBST(CXXCPP)

# set the warning flags depending on whether or not we are using gcc
if test "${GCC}" = "yes" ; then
    CFLAGS_WARNING="-Wall -Wconversion"
else
    CFLAGS_WARNING=""
fi
AC_SUBST(CFLAGS_WARNING)

#------------------------------------------------------------------------------
# If we're using GCC, see if the compiler understands -pipe.  If so, use it.
# It makes compiling go faster.  (This is only a performance feature.)
#------------------------------------------------------------------------------

if test -z "$no_pipe"; then
if test -n "$GCC"; then
  AC_MSG_CHECKING([if c++ compiler understands -pipe])
  OLDCXX="$CXX"
  CXX="$CXX -pipe"
  AC_TRY_COMPILE(,,
    AC_MSG_RESULT(yes),
    CXX="$OLDCXX"
    AC_MSG_RESULT(no))
fi
fi

AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(CXX_TEMPLATES)
]
)


#------------------------------------------------------------------------------
# AOMD: See if the user gave us the location of cca-spec
# then search $ccatut_cca_spec_dirs.
# One of these directories must be
# present and contain the correct files, or else the user
# must give the directory that contains the correct files.
#------------------------------------------------------------------------------
# cca spec (must contain java and cxx binding subdirs)

AC_DEFUN(MSQ_AOMD,
[
AC_ARG_WITH(aomd, 
    [Location of aomd library:
  --with-aomd=AOMD_LIB
                          The location of AOMD libary
                          Give the full path:
                              --with-aomd='/.../libaomd.so'
                          or check out the library from the 
                          tstt web pages. ] ,
    , [with_aomd=no])

case "$with_aomd" in
    no)
        # Default. No AOMD link. 
        with_aomd=no 
        ;;
    yes)
        #  User asked for AOMD. No path given. 
	# Looking if it is in standard Mesquite location.
	AC_CHECK_PROG(AOMD_LIB, libaomd.so, "$with_aomd"/libaomd.so, notfound, "$MSQROOT"/external/AOMD/lib/) 
        ;;
    *)
	# User gave AOMD exact location.
	AC_CHECK_PROG(AOMD_LIB, libaomd.so, "$with_aomd"/libaomd.so, notfound, "$MSQROOT"/external/AOMD/lib/)        
	AOMD_ROOT="$with_aomd" 
        ;;
esac

AOMD_ROOT="$with_aomd"

AC_SUBST(AOMD_ROOT)

AOMD_FLAG=""
if test ! "$with_aomd" = no ; then
  AOMD_FLAG="-D_MY_HASH_TABLE_"
fi
AC_SUBST(AOMD_FLAG)
]
)

#------------------------------------------------------------------------------
# DOXYGEN_PATH: See if the user gave us the location of doxygen
# then search $ccatut_dox_dirs_dirs.
# One of these directories must be
# present and contain the correct files, or else the user
# must give the directory that contains the correct files.
#------------------------------------------------------------------------------
AC_DEFUN(MSQ_PROG_DOXYGEN,
[
ccatut_dox_dirs="
    $prefix/bin/doxygen
    /usr/bin/doxygen
    /usr/local/bin/doxygen
    $HOME/doxygen/bin/doxygen
    $MSQ_HOME/doxygen
    $HOME/bin/doxygen
"
AC_ARG_WITH(dox, 
            [Location of doxygen:
             --with-dox=DoxygenLocation
             The location of doxygen. Give the full path:
                              --with-dox='/usr/local/bin/doxygen'],
    , [with_dox=_searching])

case "$with_dox" in
    no)
 	AC_MSG_WARN([Option '--without-dox'
	makes it impossible to produce source-based documentation.])
        ;;
    yes)
        #  User didn't give the option or didn't give useful
        #  information, search for it ourselves
        with_dox=_searching
        ;;
    *)
        DOXYGEN_PATH="$with_dox"
        ;;
esac

if test "$with_dox" = _searching ; then
    AC_MSG_CHECKING([for OPTIONAL doxygen])
    for dox in $ccatut_dox_dirs ; do
        if test -f $dox ; then
            with_dox=$dox
            AC_MSG_RESULT([found $dox.])
            break
        fi
    done
fi

if test "$with_dox" = _searching ; then
    AC_MSG_RESULT([Cannot find doxygen.])
    AC_MSG_WARN([Without doxygen, it is impossible to produce source-based 
	documentation.  If you wish to be able to generate this documentation,
	please re-run configure with a --with-dox=doxygen_path option.])
    with_dox="no"
fi

if test "$with_dox" = no ; then
   with_dox="@echo WARNING: doxygen not configured!"
fi

DOXYGEN_PATH="$with_dox"
AC_SUBST(DOXYGEN_PATH)
]
)

AC_SUBST(MSQ_ROOT)
AC_SUBST(MSQ_INSTALL_ROOT)


# ---------------------------------------------------------
# Decide whether to use standard C++ headers (default) 
# or old style C/C++ headers (with option --with-c-headers)
# ---------------------------------------------------------
AC_ARG_WITH(c-headers, 
    [Uses the deprecated C style for headers:
  --with-c-headers=yes
    			will use headers like <iostream.h> and <math.h>
    			instead of the new C++ style <iostream> and <cmath>. ] ,
    , [with_c_headers=no])

case "$with_c_headers" in
    no)
        # Default. Uses new C++ standard style headers.
        STD_INCLUDE_FLAG="-DUSE_STD_INCLUDES -DUSE_C_PREFIX_INCLUDES"
        ;;
    yes)
	# Uses old C style headers.
	STD_INCLUDE_FLAG=''
        ;;
esac

AC_SUBST(STD_INCLUDE_FLAG)


# --------------------------------------------------
# Checks for Makedepend (can be passed in as option).
# --------------------------------------------------
AC_ARG_WITH(makedepend,
    [Location of alternative makedepend program:
  --with-makedepend=EXECUTABLE
                          The location of makedepend.
                          Give the full path:
                              --with-makedepend='/usr/local/bin/makedepend' ],
    , [with_makedepend=yes])

case "$with_makedepend" in
    no)
	# no check for makedepend
	AC_MSG_WARN([No makedepend given. Will not generate Makefile dependencies for your installation ... or please use --with-makedepend=[path] option.])
        ;;
    yes)
        # search for it ourselves
	AC_CHECK_PROG(MAKEDEPEND, makedepend, makedepend, nomakedepend, $PATH, :)dnl
        ;;
    *)
	# Checks path given by user. 
	AC_CHECK_PROG(MAKEDEPEND,"$with_makedepend","$with_makedepend", nomakedepend, /., :)dnl
	if test "$MAKEDEPEND" = "nomakedepend" ; then
	  AC_MSG_ERROR([option --with-makedepend='...' has been given an invalid executable.])
	fi
        ;;
esac

#  print a warning if none found.
if test "$MAKEDEPEND" = nomakedepend ; then
   AC_MSG_WARN([Needs makedepend to generate dependencies for your installation. You need the dependencies for a robust (re)compilation.])
   AC_MSG_WARN([Please try to use --with-makedepend='/PATH/makedepend'.])
fi
AC_SUBST(MAKEDEPEND)
touch make.dependencies
# ----------------------------------------------------------------


#-----------------------------------------------------------------
#  see if the user gave us the location of a prefered archiver
#-----------------------------------------------------------------
AC_ARG_WITH(archiver,
    [Location of alternative archiver (default is 'ar ru'):
  --with-archiver=EXECUTABLE
                          The location of the prefered archiver.
                          Give the full path and arguments:
                              --with-archiver='/usr/local/bin/ar ru' ],
    , [with_archiver=yes])

case "$with_archiver" in
    no)
        #  User specified --without-archiver
        #  print a warning.
        AC_MSG_WARN([Option '--without-archiver' Causes linking to be suppressed.])
        AC_MSG_ERROR([An archiver is required.])
        ;;
    yes)
        # search for it ourselves
	AC_CHECK_PROG(AR, "ar", "ar", noarchiver, $PATH, :)dnl
        ;;
    *)
	# Checks path given by user. 
	AC_CHECK_PROG(AR,"$with_archiver","$with_archiver", noarchiver, /., :)dnl
	if test "$AR" = "noarchiver" ; then
	  AC_MSG_ERROR([option --with-archiver='...' has been given an invalid executable.])
	fi
        ;;
esac

#  print a warning if none found.
if test "$AR" = noarchiver ; then
   AC_MSG_ERROR([Needs archiver to generate library used in code linking. Please use --with-archiver='/PATH/archiver' option to give a valid archiver.])
fi
if test "$AR" = ar ; then
  AR="ar ru" # space between ar and ru would mess up AC_CHECK_PROG ... so we deal with it here.  
fi
AC_SUBST(AR)

# -----------------------------------------------------------


MSQ_UNIX_PROGRAMS
MSQ_COMPILER
MSQ_AOMD
MSQ_PROG_DOXYGEN


GENFILES="Makefile.customize"

AC_SUBST($GENFILES)
AC_OUTPUT($GENFILES)




#touch make.dependencies
#if [ -d ../driver ]; then
#  touch ../driver/make.dependencies
#fi
